apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

compileJava {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

group = 'com.droidkit'
version = '1.1'

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

if (project.hasProperty("ossrhUsername") && project.hasProperty("ossrhPassword")) {

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'DroidKit Bser'
                    packaging 'jar'
                    description 'DroidKit Bser - protobuf compatible serialization library.'
                    url 'https://github.com/actorapp/droidkit-bser'

                    scm {
                        url 'scm:git@github.com:actorapp/droidkit-bser.git'
                        connection 'scm:git@github.com:actorapp/droidkit-bser.git'
                        developerConnection 'scm:git@github.com:actorapp/droidkit-bser.git'
                    }

                    licenses {
                        license {
                            name 'The MIT License (MIT)'
                            url 'http://opensource.org/licenses/MIT'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'ex3ndr'
                            name 'Stepan Korshakov'
                        }
                    }
                }
            }
        }
    }
}